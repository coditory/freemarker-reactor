apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'io.github.gradle-nexus.publish-plugin'

jar {
    from(rootProject.projectDir) {
        include "LICENSE"
        into "META-INF"
    }
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = archivesBaseName
            from components.java
            artifact sourcesJar
            artifact javadocJar

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = archivesBaseName
                description = project.description
                url = 'https://github.com/coditory/quark-config'
                organization {
                    name = "Coditory"
                    url = "https://coditory.com"
                }
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'coditory'
                        name = 'Coditory'
                        email = 'admin@coditory.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:coditory/quark-config.git'
                    developerConnection = 'scm:git@github.com:coditory/freemarker-reactor.git'
                    url = 'https://github.com/coditory/freemarker-reactor'
                }
                issueManagement {
                    system = "GitHub"
                    url = "https://github.com/coditory/freemarker-reactor/issues"
                }
            }
        }
    }
}

signing {
    if (System.getenv('SIGNING_KEY') && System.getenv('SIGNING_PASSWORD')) {
        useInMemoryPgpKeys(System.getenv('SIGNING_KEY'), System.getenv('SIGNING_PASSWORD'))
    }
    sign publishing.publications.mavenJava
}

nexusPublishing {
    connectTimeout = Duration.ofMinutes(5)
    clientTimeout = Duration.ofMinutes(5)
    repositories {
        sonatype {
            username = project.findProperty('nexusUsername') ?: System.getenv('NEXUS_USERNAME')
            password = project.findProperty('nexusPassword') ?: System.getenv('NEXUS_PASSWORD')
        }
    }
}
